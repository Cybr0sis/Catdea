/*
 * Copyright 2019 Cybrosis
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

{
    parserClass = "com.cybrosis.catdea.lang.CatdeaParser"
    extends = "com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix = "PsiCatdea"
    psiImplClassSuffix = "Impl"
    psiPackage = "com.cybrosis.catdea.lang.psi"
    psiImplPackage = "com.cybrosis.catdea.lang.psi.impl"
    
    elementTypeHolderClass = "com.cybrosis.catdea.lang.CatdeaTypes"
    elementTypeClass = "com.cybrosis.catdea.lang.psi.CatdeaElementType"
    tokenTypeClass = "com.cybrosis.catdea.lang.lexer.CatdeaTokenType"

    classHeader='generatedFilesHeader.txt'
    
    tokens = [
        COLON = ": "
        SLASH = "/"
        COMMENT= "regexp:#(.*(\R\s+)?)*"
        TIMESTAMP_TOKEN
        PID_TOKEN
        LEVEL_TOKEN
        PACKAGE_TOKEN
        TAG_TOKEN
        MESSAGE_TOKEN
        BUFFER
    ]
}

file ::= (buffer | line)*

private line ::= entry | MESSAGE_TOKEN

buffer ::= BUFFER line* {
    pin=1
}

entry ::= header message {
    mixin="com.cybrosis.catdea.lang.psi.impl.PsiCatdeaEntryMixin"
    methods=[getLevel getTag getMsg]
    pin=1
}

header ::= info level tag COLON {
    pin=4
}

private info ::= [TIMESTAMP_TOKEN] [PID_TOKEN [SLASH PACKAGE_TOKEN]]

level ::= LEVEL_TOKEN {
    mixin="com.cybrosis.catdea.lang.psi.impl.PsiCatdeaLevelMixin"
    methods=[getLevel]
}

tag ::= TAG_TOKEN

message ::= MESSAGE_TOKEN+
